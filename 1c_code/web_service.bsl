Функция СформироватьJSON(Данные)

	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, Данные);
	Результат = ЗаписьJSON.Закрыть();

	Возврат Результат;

КонецФункции

Функция FindUser(tg_id)

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Пользователи.Ссылка,
		|	КонтактнаяИнформация.Представление,
		|	Пользователи.Наименование
		|ИЗ
		|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО ((ВЫРАЗИТЬ(КонтактнаяИнформация.Объект КАК Справочник.Пользователи)) = Пользователи.Ссылка)
		|ГДЕ
		|	КонтактнаяИнформация.Тип = &Тип
		|	И КонтактнаяИнформация.Вид = &Вид
		|	И (ВЫРАЗИТЬ(КонтактнаяИнформация.Представление КАК СТРОКА(20))) = &ТГ_ИД";

	Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.Другое);
	Запрос.УстановитьПараметр("Вид", Справочники.ВидыКонтактнойИнформации.НайтиПоНаименованию("telegram_id"));
	Запрос.УстановитьПараметр("ТГ_ИД", tg_id);

	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		РезСтруктура = Новый Структура("Status, Text", "Error", "Не удалось найти пользователя!")
	Иначе
		РезСтруктура = Новый Структура("Status", "OK");

		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

		МассивСостояний = Новый Массив;
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			РезСтруктура.Вставить("Text", ВыборкаДетальныеЗаписи.Наименование);
		КонецЕсли;

	КонецЕсли;

	Результат = СформироватьJSON(РезСтруктура);
	Возврат Результат;

КонецФункции

Функция DocumentState(DocNumber)

	НомерПодбора = Число(DocNumber);

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СостояниеМаршрута.Период КАК Период,
		|	СостояниеМаршрута.Состояние
		|ИЗ
		|	РегистрСведений.СостояниеМаршрута КАК СостояниеМаршрута
		|ГДЕ
		|	СостояниеМаршрута.Подбор.Номер = &Номер
		|	И СостояниеМаршрута.Подбор.Дата >= &Дата
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период УБЫВ";

	Запрос.УстановитьПараметр("Дата", НачалоГода(ТекущаяДата()));
	Запрос.УстановитьПараметр("Номер", Число(НомерПодбора));

	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		РезСтруктура = Новый Структура("Status, Text", "Error", "Не удалось найти маршрут!")
	Иначе
		РезСтруктура = Новый Структура("Status", "OK");

		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

		МассивСостояний = Новый Массив;
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			МассивСостояний.Добавить(Новый Структура("Date, State", Формат(ВыборкаДетальныеЗаписи.Период, "ДЛФ=DT"), Строка(ВыборкаДетальныеЗаписи.Состояние)));
		КонецЦикла;

		РезСтруктура.Вставить("StateList", МассивСостояний);
	КонецЕсли;

	Результат = СформироватьJSON(РезСтруктура);
	Возврат Результат;

КонецФункции

Функция ChangeDocumentState(State, DocNumber)

	Состояние = Перечисления.СостоянияМаршрута[State];
	НомерПодбора = Число(DocNumber);
	Отказ = Ложь;

	Подбор = Документы._Подбор.НайтиПоНомеру(НомерПодбора, НачалоГода(ТекущаяДата()));

	Если Подбор = Документы._Подбор.ПустаяСсылка() Тогда
		Отказ = Истина;
	Иначе
		Охта.УстановитьСтатусРейса(Подбор, Состояние, , Отказ);
	КонецЕсли;

	Если Отказ Тогда
		РезСтруктура = Новый Структура("Status, Text", "Error", "Не удалось установить состояние маршрута!");
		Результат = СформироватьJSON(РезСтруктура);
	Иначе
		Результат = DocumentState(НомерПодбора);
	КонецЕсли;

	Возврат Результат

КонецФункции
